/**
* Player sidekick (Parabo or Disk-kun) controllable with joypad 2 and 3. These need to be unlocked by finding them in the game
*
*/
.include "src/class/lifeform/lifeform.sidekick.h"
.section "lifeform.sidekick"

  METHOD init
  lda OBJECT.CALL.ARG.1,s
  sta.b dimension.position.x + 1

  lda OBJECT.CALL.ARG.2,s
  sta.b dimension.position.y + 1

  lda #SPRITE.PLAYER.DEFAULT.SPEED*1.2
  sta lifeform.speed

  sep #$20
  lda OBJECT.CALL.ARG.5,s
  sta.b lifeform.controller.id

  lda #LIFEFORM.CONTROLLER.HUMAN.SIDEKICK
  sta.b lifeform.controller.type
  rep #$31

  lda OBJECT.CALL.ARG.6,s
  sta lifeform.animation.base
  jsr lifeform.player.init

  lda.w #4
  sta lifeform.bouncyness

  pea HUD.SIDEKICK.STATE.ON
  pei (lifeform.controller.id)
  CALL background.hud.setPlayerState.MTD this.obj.hud
  pla
  pla

  lda.w #SPRITE.combo_ready.PTR
  ldx.w #0
  ldy.w #0
  jsl extern.lifeform.sprite  
  rts

  METHOD play
  stz lifeform.controller.current.actions
  lda lifeform.target.properties
  jsr lifeform.library.evaluate.alwaysOn

  lda lifeform.controller.current.actions
  jsl extern.lifeform.movement.evaluate
  jsl extern.lifeform.animate

  jsr lifeform.player.checkHit
  jsl extern.lifeform.checkBerzerkGet
  bcc +
    lda.w #AUDIO.EFFECT.COIN_HIGH
    jmp interface.dimension.playSoundEffect

+  
  jsr _checkPlayerWarp
  jmp lifeform.player.updateHud

_checkPlayerWarp:
  AX16
  lda animation.timer
  inc a
  inc a
  inc a
  and.w #$f
  bne +  
    lda iterator.properties
    bit.w #OBJECT.PROPERTIES.isOnScreen
    bne +
      lda lifeform.state
      and.w #$ff
      cmp.w #LIFEFORM.STATE.GRABBED
      beq +    
          lda.w (GLOBAL.screen.position.x &$ffff) +1
          clc
          adc.w #SCREEN.SIZE.X/2
          sec
          sbc dimension.position.x +1
          SCRIPT_JSR core.math.abs
          pha
          lda.w (GLOBAL.screen.position.y &$ffff) +1
          clc
          adc.w #SCREEN.SIZE.Y/2
          sec
          sbc dimension.position.y +1
          SCRIPT_JSR core.math.abs
          clc
          adc 1,s
          sta 1,s
          pla
          cmp.w #SIDEKICK.OFFSCREEN.WARP_THRESHOLD
          bcc +
            jsl extern.lifeform.warpToPlayer

+  
  rts


  METHOD pause
  jsl extern.lifeform.setEvaluateWait
  jmp sprite.library.pauseAnimation

  METHOD kill
  pea HUD.SIDEKICK.STATE.OFF
  pei (lifeform.controller.id)
  CALL background.hud.setPlayerState.MTD this.obj.hud
  pla
  pla

  jmp sprite.library.kill

  ;custom, make sure to immediately set focus after external position change (so camera doesn't scroll over whole playfield)
 METHOD setPosition
  jmp lifeform.player.setPosition


  METHOD oncommand
  lda OBJECT.CALL.ARG.1,s
  cmp #LIFEFORM.COMMAND.THROW
  bne +
    jsl extern.lifeform.onThrow
    rts

+  
  cmp #LIFEFORM.COMMAND.ATTACK
  bne +
    lda lifeform.invincibility.timer
    and.w #$ff
    bne +

    ora lifeform.invincibility.invisible.timer
    and.w #$ff
    bne +
    lda lifeform.state
    and.w #$ff
    cmp.w #LIFEFORM.STATE.SUICIDE
    beq +
    cmp.w #LIFEFORM.STATE.SUPERPUNCH
    beq +
      lda OBJECT.CALL.ARG.2,s
      sta iterator.target+2
      lda OBJECT.CALL.ARG.3,s
      sta iterator.target
      jmp lifeform.player.getHurt

+

  lda OBJECT.CALL.ARG.1,s
  cmp #LIFEFORM.COMMAND.GRAB
  bne +
    jsl extern.lifeform.onGrab
    rts

+
  cmp.w #LIFEFORM.COMMAND.KNOCK_OUT
  bne +
    jsl extern.lifeform.onKnockOut
    rts

+
  cmp.w #LIFEFORM.COMMAND.SQUASH
  bne +
    stz lifeform.berzerk.counter
    sec
    rts

+
  clc
  rts


  METHOD disable
  jmp sprite.library.disable

  METHOD enable
  jsl extern.lifeform.setEvaluateWait
  jmp sprite.library.enable

  METHOD getAnimationBase
  lda lifeform.animation.base
  rts

  CLASS lifeform.sidekick oncommand getAnimationBase

.ends	
