/**
* Spiked mine enemy that jumps
* 
*/

.include "src/class/lifeform/lifeform.spiked_mine.h"
.section "lifeform.spiked_mine"

  METHOD init
  lda.w #SPRITE.spiked_mine.PTR
  ldx.w #LIFEFORM.ABILITIES.JUMP
  jsl extern.lifeform.initialize

  lda.w #2
  sta lifeform.bouncyness

  lda #lifeform.turn.CLB
  sta lifeform.callback.touch.wall
  lda #spiked_mine.jump.CLB
  sta lifeform.callback.touch.floor
  rts

  CALLBACK spiked_mine.jump
  sep #$20
  lda #LIFEFORM.STATE.JUMPING
  sta lifeform.state

  lda #LIFEFORM.CONDITION.AIRBORNE
  sta lifeform.condition

  lda #LIFEFORM.JUMP.AGE
  sta lifeform.action.age
  rep #$31

  lda.w #AUDIO.EFFECT.DASH
  jsr interface.dimension.playSoundEffect


  ldx #SPIKED_MINE.JUMP.IMPULSE
  lda this.jump.count
  bne +
    ldx #SPIKED_MINE.JUMP.IMPULSE*8

+
  txa
  sta dimension.speed.y

  ldx #SPIKED_MINE.JUMP.ACCEL
  lda this.jump.count
  bne +
    ldx #SPIKED_MINE.JUMP.ACCEL*8

+
  txa  
  sta dimension.acceleration.y


  lda this.jump.count
  inc a
  and.w #$1
  sta this.jump.count

  ;overcome ground traction
  dec dimension.position.y+1

  lda.w #SPRITE.dust.PTR
  ldx.w #0
  ldy.w #10
  jsl extern.lifeform.sprite
  rts


  METHOD play
  ;if being thrown (e.g. by last boss), die after some time
  lda this.deathTimer
  beq +
    dec this.deathTimer
    bne +
      jsl extern.movement.explodeEnemy
      rts

+  
  jsl extern.lifeform.evalOffscreen
  rts

  METHOD pause
  jmp sprite.library.pauseAnimation

  METHOD kill
  jmp sprite.library.kill


  METHOD oncommand
  AX16
  lda OBJECT.CALL.ARG.1,s
  cmp #LIFEFORM.COMMAND.ATTACK
  bne +  
    lda.w #AUDIO.EFFECT.BLOCK
    jsr interface.dimension.playSoundEffect
    clc
    rts

+
  cmp #LIFEFORM.COMMAND.THROW
  bne +
    lda.w #SPIKED_MINE.DEATH.TIMER
    sta this.deathTimer
    jsl extern.lifeform.onThrow
    sec
    rts

+
  clc
  rts


  CLASS lifeform.spiked_mine oncommand

.ends	
	
